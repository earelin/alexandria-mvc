plugins {
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.6'
    id 'io.freefair.lombok' version '6.4.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
    id 'java'
    id 'org.springframework.boot' version '2.6.4'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation project(':domain')
    implementation project(':infrastructure')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.data:spring-data-commons'

    implementation 'org.mapstruct:mapstruct:1.4.2.Final'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:spring-mock-mvc:4.5.1'

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

checkstyle {
    toolVersion '10.0'
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

spotbugs {
    ignoreFailures = true
}

spotbugsMain {
    reports {
        xml.enabled = true
    }
}

spotbugsTest {
    reports {
        xml.enabled = true
    }
}
